plugins {
    id 'java'
    id 'org.kordamp.gradle.jandex' version '0.6.0'
    id 'application'
}

group = 'com.github.liudi0105'
version = '1.0'

ext {
    mainClass = 'io.helidon.microprofile.cdi.Main'
}

repositories {
    allprojects {
        repositories {
            maven { url "https://maven.aliyun.com/repository/public" }
        }
    }
}

dependencies {
    implementation platform("com.github.liudi0105:summer-boot-dependencies")
    implementation 'io.helidon.microprofile.bundles:helidon-microprofile'
    implementation 'org.glassfish.jersey.media:jersey-media-json-binding'
    runtimeOnly 'io.smallrye:jandex'
    runtimeOnly 'jakarta.activation:jakarta.activation-api'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('copyLibs', Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/libs'
}

jar {
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes('Main-Class': "${mainClass}",
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}


tasks.register('moveBeansXML') {
    doLast {
        ant.move file: "${buildDir}/resources/main/META-INF/beans.xml",
                todir: "${buildDir}/classes/java/main/META-INF"
    }
}
compileTestJava.dependsOn jandex
jar.dependsOn jandex
test.dependsOn moveBeansXML
run.dependsOn moveBeansXML
copyLibs.dependsOn jar
assemble.dependsOn copyLibs
run.dependsOn jandex